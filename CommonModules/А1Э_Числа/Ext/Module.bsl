Функция Модуль(Число) Экспорт
	Если Число < 0 Тогда
		Возврат - Число;
	Иначе
		Возврат ЧИсло;
	КонецЕсли;
КонецФункции

Функция ОписаниеТипа(Длина, ДлинаДробнойЧасти = 0) Экспорт
	Возврат Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(Длина, ДлинаДробнойЧасти));	
КонецФункции

Функция ВЧисло(Знач Значение) Экспорт
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		Возврат Значение;
	ИначеЕсли Значение = Неопределено Тогда
		Возврат 0;
	ИначеЕсли ТипЗнч(Значение) = Тип("Строка") Тогда
		Значение = СтрЗаменить(Значение, " ", "");
		Значение = СтрЗаменить(Значение, Символы.НПП, "");
		Возврат Число(Значение);
	Иначе
		А1Э_Служебный.ИсключениеНеверныйТип("Значение", "А1Э_Числа.ВЧисло", Значение, "Число,Неопределено");
	КонецЕсли;
КонецФункции

Функция УменьшениеОстатка(Остаток, Уменьшение) Экспорт
	Если Остаток <= 0 Тогда
		Возврат 0;
	ИначеЕсли Остаток > Уменьшение Тогда
		Остаток = Остаток - Уменьшение;
		Возврат Уменьшение;
	Иначе
		РеальноеУменьшение = Остаток;
		Остаток = 0;
		Возврат РеальноеУменьшение;
	КонецЕсли;
КонецФункции

Функция НеНоль(Число, ВместоНуля = 1) Экспорт
	Если Число = 0 Тогда
		Возврат ВместоНуля;
	КонецЕсли;
	Возврат Число;
КонецФункции

Функция ЯвляетсяЦелым(Значение) Экспорт
	Если ТипЗнч(Значение) <> Тип("Число") Тогда Возврат Ложь; КонецЕсли;
	Возврат Цел(Значение) = Значение;
КонецФункции

Функция ЯвляетсяЦелымНеотрицательным(Значение) Экспорт
	Если НЕ ЯвляетсяЦелым(Значение) Тогда Возврат Ложь; КонецЕсли;
	Возврат Значение >= 0;		
КонецФункции

Функция ЯвляетсяНатуральным(Значение) Экспорт
	Если НЕ ЯвляетсяЦелым(Значение) Тогда Возврат Ложь; КонецЕсли;
	Возврат Значение > 0;
КонецФункции

Функция Логарифм(Число, Основание) Экспорт
	Возврат Log(Число)/Log(Основание);
КонецФункции

#Область НаибольшийОбщийДелитель

Функция НаибольшийОбщийДелитель(П1, П2 = Null, П3 = Null, П4 = Null, П5 = Null, П6 = Null, П7 = Null, П8 = Null, П9 = Null, П10 = Null) Экспорт
	Массив = А1Э_Массивы.ИзПараметров(П1, П2, П3, П4, П5, П6, П7, П8, П9, П10);
	Если Массив.Количество() = 0 Тогда А1Э_Служебный.СлужебноеИсключение("Наибольший общий делитель пустого множества не определен!"); КонецЕсли;
	НОД = Массив[0];
	Для Сч = 1 По Массив.Количество() - 1 Цикл
		НОД = НаибольшийОбщийДелительДвух(НОД, Массив[Сч]);
	КонецЦикла;
	Возврат НОД;
КонецФункции

Функция НаибольшийОбщийДелительДвух(Число1, Число2) Экспорт
	Если НЕ ЯвляетсяНатуральным(Число1) Или НЕ ЯвляетсяНатуральным(Число2) Тогда
		А1Э_Служебный.СлужебноеИсключение("Наибольший общий делитель определен только для натуральных чисел!");
	КонецЕсли;
	Если Число1 > Число2 Тогда
		БольшееЧисло = Число1;
		МеньшееЧисло = Число2;
	Иначе
		БольшееЧисло = Число2;
		МеньшееЧисло = Число1;
	КонецЕсли;
	
	Возврат НаибольшийОбщийДелительРекурсияЕвклида(БольшееЧисло, МеньшееЧисло);
КонецФункции

Функция НаибольшийОбщийДелительРекурсияЕвклида(БольшееЧисло, МеньшееЧисло)
	Остаток = БольшееЧисло%МеньшееЧисло;
	Если Остаток = 0 Тогда Возврат МеньшееЧисло; КонецЕсли;
	Возврат НаибольшийОбщийДелительРекурсияЕвклида(МеньшееЧисло, Остаток);		
КонецФункции

#КонецОбласти 


