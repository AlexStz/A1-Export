Функция ПроверитьПредопределенныеДанные() Экспорт
	Если Метаданные.Справочники.Найти("А1_Предопределенные") = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Предопределенные.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.А1_Предопределенные КАК Предопределенные
	|ГДЕ
	|	Предопределенные.Значение = НЕОПРЕДЕЛЕНО");
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Истина;
	КонецЕсли;
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		А1Э_Сообщения.СообщитьПоШаблону(
		"Предопределенное значение [Ссылка] не заполнено! Программа может работать некорректно!",
		Новый Структура("Ссылка", Выборка.Ссылка));
	КонецЦикла;
	Возврат Ложь;
КонецФункции

#Область ПриСозданииНаСервере

Функция ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	ЗаполнитьПустоеЗначениеПоПредопределенномуТипу(Форма)	
КонецФункции

Функция ЗаполнитьПустоеЗначениеПоПредопределенномуТипу(Форма)
	Если Форма.Объект.Значение <> Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	ИмяПредопределенныхДанных = Форма.Объект.ИмяПредопределенныхДанных;
	Если СтрНайти(ИмяПредопределенныхДанных, "__") = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	ИмяОбъекта = А1Э_Строки.Перед(ИмяПредопределенныхДанных, "__");
	НРЕГ = НРЕГ(ИмяОбъекта);
	
	Если НРЕГ = "строка" Тогда
		Значение = "";
	ИначеЕсли НРЕГ = "число" Тогда
		Значение = 0;       
	ИначеЕсли НРЕГ = "булево" Тогда
		Значение = Ложь;
	ИначеЕсли НРЕГ = "дата" Тогда
		Значение = '00010101000000';
	Иначе
		Попытка
			Значение = А1Э_СтандартныеТипы.ПустаяСсылкаПолучить(ИмяОбъекта);
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Значение = Неопределено;
		КонецПопытки;
	КонецЕсли;
	Форма.Объект.Значение = Значение;	
КонецФункции 

#КонецОбласти 
