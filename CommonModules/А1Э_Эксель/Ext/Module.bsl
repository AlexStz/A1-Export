#Область ПрограммныйИнтерфейс

// Функция - Файл эксель в массив структур
//
// Параметры:
//  ПутьКФайлу	 - Строка - полный путь к файлу 
//  Параметры	 - Структура - Содержит ключи
//		ОбластьДанных - Структура - Может содержать ключи "ПерваяСтрока", "ПоследняяСтрока", "ПерваяКолонка", "ПоследняяКолонка" с числовыми значениями.
//		СтрокиЗаголовков - Массив, Число - Содержит массив номеров строк (числа), в которых хранятся заголовки колонок.   
// 
// Возвращаемое значение:
//  Массив - массив структур, имитирующий работу таблицы. 
//
Функция ФайлЭксельВТаблицу(ПутьКФайлу, Параметры) Экспорт
	
	РезультатВыполнения = ОткрытьЭксель(ПутьКФайлу);
	Если НЕ А1Э_РезультатыФункций.ОбработатьОшибки(РезультатВыполнения, "Запуск Misrosoft Excel")  Тогда
		Возврат Неопределено;
	КонецЕсли;
	Эксель = РезультатВыполнения.Значение;
	
	Попытка
		РабочиеПараметры = Новый Структура;
		РабочиеПараметры.Вставить("ОбластьДанных", ПолучитьОбластьДанных(Эксель, Параметры));
		РабочиеПараметры.Вставить("СтрокиЗаголовков", ПолучитьМассивСтрокЗаголовков(Параметры));
		РабочиеПараметры.Вставить("МассивКолонок", ПолучитьМассивКолонок(Эксель, РабочиеПараметры));
		
		Таблица = Новый Массив;
		Если А1Э_Структуры.ЗначениеСвойства(Параметры, "ИспользоватьНовыйМетод") = Истина Тогда
			ОбластьДанных = РабочиеПараметры.ОбластьДанных;
			Область = Эксель.Range(Эксель.Cells(ОбластьДанных.ПерваяСтрока, ОбластьДанных.ПерваяКолонка), Эксель.Cells(ОбластьДанных.ПоследняяСтрока,ОбластьДанных.ПоследняяКолонка));
			Данные = Область.Value.Выгрузить();
			//ТУДУ - Доделать!			
		Иначе
			НомерСтроки = РабочиеПараметры.ОбластьДанных.ПерваяСтрока - 1;
			Пока НомерСтроки < РабочиеПараметры.ОбластьДанных.ПоследняяСтрока Цикл
				НомерСтроки = НомерСтроки + 1;
				Таблица.Добавить(ПрочитатьСтроку(Эксель, РабочиеПараметры.МассивКолонок, НомерСтроки));
			КонецЦикла;
		КонецЕсли;
	
		ЗакрытьЭксель(Эксель);
		
		Возврат Таблица;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		ЗакрытьЭксель(Эксель);
	КонецПопытки;
	
КонецФункции

#Область ФайлЭксельВТаблицу

Функция ПолучитьОбластьДанных(Эксель, Параметры)
	ОбластьДанных = НовыйОбластьДанных();
	А1Э_Структуры.СкопироватьСвойства(ОбластьДанных, Параметры.ОбластьДанных, Истина);
	
	ПоследняяСтрока = Эксель.Cells(1,1).SpecialCells(11).Row;
	Если ОбластьДанных.ПоследняяСтрока = 0 ИЛИ ОбластьДанных.ПоследняяСтрока > ПоследняяСтрока Тогда
		ОбластьДанных.ПоследняяСтрока = ПоследняяСтрока;
	КонецЕсли;
	
	ПоследняяКолонка = Эксель.Cells(1,1).SpecialCells(11).Column;
	Если ОбластьДанных.ПоследняяКолонка = 0 ИЛИ ОбластьДанных.ПоследняяКолонка > ПоследняяКолонка Тогда
		ОбластьДанных.ПоследняяКолонка = ПоследняяКолонка;
	КонецЕсли;
	
	Возврат ОбластьДанных;
КонецФункции

Функция ПолучитьМассивСтрокЗаголовков(Параметры)
	СтрокиЗаголовков = А1Э_Структуры.ЗначениеСвойства(Параметры, "СтрокиЗаголовков");
	
	Если СтрокиЗаголовков = Неопределено Тогда
		МассивСтрокЗаголовков = А1Э_Массивы.Создать(1);
	ИначеЕсли ТипЗнч(СтрокиЗаголовков) = Тип("Число") Тогда
		МассивСтрокЗаголовков = А1Э_Массивы.Создать(СтрокиЗаголовков);
	ИначеЕсли ТипЗнч(СтрокиЗаголовков) = Тип("Массив") Тогда
		МассивСтрокЗаголовков = А1Э_Массивы.Скопировать(СтрокиЗаголовков);
	КонецЕсли;
	#Если Сервер И НЕ Сервер Тогда
		МассивСтрокЗаголовков = Новый Массив;		
	#КонецЕсли 
	А1Э_Массивы.Сортировать(МассивСтрокЗаголовков);
	Возврат МассивСтрокЗаголовков;	
КонецФункции

Функция ПолучитьМассивКолонок(Эксель, РабочиеПараметры)
	МассивКолонок = Новый Массив;
	КоличестваИдентификаторов = Новый Соответствие;
	НомерКолонки = РабочиеПараметры.ОбластьДанных.ПерваяКолонка - 1;
	Пока НомерКолонки < РабочиеПараметры.ОбластьДанных.ПоследняяКолонка Цикл
		НомерКолонки = НомерКолонки + 1;
		СтруктураКолонки = НовыйСтруктураКолонки();
		СтруктураКолонки.Номер = НомерКолонки;
		НаименованиеКолонки = "";
		Для Каждого СтрокаЗаголовка Из РабочиеПараметры.СтрокиЗаголовков Цикл
			ТекстЯчейки = ПрочитатьЯчейку(Эксель, СтрокаЗаголовка, НомерКолонки);
			Если ТекстЯчейки <> "" Тогда
				НаименованиеКолонки = НаименованиеКолонки + ТекстЯчейки + "_";
			КонецЕсли;
		КонецЦикла;
		НаименованиеКолонки = А1Э_Строки.Обрубить(НаименованиеКолонки, 1);
		Идентификатор = А1Э_СтандартныеТипы.Идентификатор(НаименованиеКолонки, "Колонка_" + А1Э_Строки.ВСтроку(НомерКолонки));
		СтруктураКолонки.Имя = А1Э_СтандартныеТипы.ИдентификаторБезПовторений(Идентификатор, КоличестваИдентификаторов);
		МассивКолонок.Добавить(СтруктураКолонки);
	КонецЦикла;
	Возврат МассивКолонок;	
КонецФункции

#КонецОбласти

#КонецОбласти 

#Область ИнтерфейсЭксель

Функция ОткрытьЭксель(ПутьКФайлу) Экспорт 
	Попытка
		Эксель = Новый COMОбъект("Excel.Application");
		Эксель.WorkBooks.Open(ПутьКФайлу);
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Возврат А1Э_РезультатыФункций.Ошибка("Ошибка при открытии программы Microsoft Excel:" + Символы.ПС + ОписаниеОшибки);
	КонецПопытки;
	
	Попытка
		Эксель.Sheets(1).Select(); 
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		ЗакрытьЭксель(Эксель);
		Возврат А1Э_РезультатыФункций.Ошибка("Ошибка при открытии чтении файла " + ПутьКФайлу + ":" + Символы.ПС + ОписаниеОшибки);
	КонецПопытки;
	
	Возврат А1Э_РезультатыФункций.Успех(Эксель);
КонецФункции 

Функция ЗакрытьЭксель(Эксель) Экспорт
	Если Эксель = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Эксель.ActiveWorkbook.Close();
	Эксель.DisplayAlerts = 0; 
	Эксель.Quit();
	Эксель.DisplayAlerts = 1;
	Эксель = Неопределено;
КонецФункции 

Функция ПрочитатьЯчейку(Эксель, Строка, Столбец) Экспорт
	Возврат Эксель.Cells(Строка, Столбец).Text;	
КонецФункции

Функция ПрочитатьСтроку(Эксель, МассивКолонок, НомерСтроки) Экспорт
	СтруктураСтроки = Новый Структура;
	СтруктураСтроки.Вставить("НомерСтрокиА1", НомерСтроки);
	Для Каждого СтруктураКолонки Из МассивКолонок Цикл
		Значение = ПрочитатьЯчейку(Эксель, НомерСтроки, СтруктураКолонки.Номер);
		СтруктураСтроки.Вставить(СтруктураКолонки.Имя, Значение); 
	КонецЦикла;
	Возврат СтруктураСтроки;
КонецФункции

#КонецОбласти

#Область Декларации

Функция НовыйОбластьДанных()
	Возврат Новый Структура("ПерваяСтрока,ПоследняяСтрока,ПерваяКолонка,ПоследняяКолонка",1,0,1,0);
КонецФункции

Функция НовыйСтруктураКолонки()
	Возврат Новый Структура("Имя,Номер");	
КонецФункции

#КонецОбласти