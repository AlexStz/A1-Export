Функция ЛичнаяВременнаяПапка() Экспорт
	Возврат А1Э_ПовторноеИспользование.РезультатФункции("А1Э_ФайловаяСистема.ЛичнаяВременнаяПапкаБезПовтИсп");
КонецФункции

Функция ЛичнаяВременнаяПапкаБезПовтИсп() Экспорт 
	WSH = Новый COMОбъект("Wscript.shell");
	Путь = "" + WSH.ExpandEnvironmentStrings("%temp%");
	Путь = А1Э_Строки.ЗавершитьПодстрокой(Путь, "\");
	Возврат Путь;	
КонецФункции 

Функция ПолучитьПутьКФайлу(Файл, Расширение = "") Экспорт
	Если ТипЗнч(Файл) = Тип("Строка") Тогда
		Возврат Файл;
	ИначеЕсли ТипЗнч(Файл) = Тип("ДвоичныеДанные") Тогда
		Возврат ПоместитьФайлВоВременнуюПапку(Файл, Расширение);
	Иначе
		А1Э_Служебный.ИсключениеНеверныйТип("Файл", "ПолучитьПутьКФайлу", Файл, "Строка, ДвоичныеДанные");
	КонецЕсли;
	
КонецФункции

Функция ПоместитьФайлВоВременнуюПапку(ДвоичныеДанные, Расширение) Экспорт
	
	Попытка
		ПолныйПутьКФайлу = ПутьКНовомуВременномуФайлу(Расширение); 
		ДвоичныеДанные.Записать(ПолныйПутьКФайлу);				
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		А1Э_Служебный.СлужебноеИсключение("Не удалось записать файл " + ОписаниеОшибки);      
	КонецПопытки;            
	
	Возврат ПолныйПутьКФайлу;
КонецФункции

Функция ПутьКНовомуВременномуФайлу(Расширение) Экспорт
	ПутьКВременнойПапке = А1Э_ФайловаяСистема.ЛичнаяВременнаяПапка();
	Если ЗначениеЗаполнено(ПутьКВременнойПапке) Тогда 
		ПолныйПутьКФайлу = ПутьКВременнойПапке + "ВременныйФайл" + Новый УникальныйИдентификатор + "." + Расширение;
		Возврат ПолныйПутьКФайлу;
	Иначе
		А1Э_Служебный.СлужебноеИсключение("Не удалось получить путь ко временной папке!");
	КонецЕсли;
КонецФункции 
	
	
