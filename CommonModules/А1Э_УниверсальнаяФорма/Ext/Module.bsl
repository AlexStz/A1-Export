#Если НЕ Клиент Тогда

	Функция ПриСозданииНаСервере(Форма, СтандартнаяОбработка) Экспорт  
		МассивОписаний = Новый Массив;
		А1Э_Формы.ДобавитьОписаниеРеквизита(МассивОписаний, "А1_УниверсальнаяФорма__ОбработчикиЭлементов", "");
		А1Э_Формы.ДобавитьОписаниеРеквизита(МассивОписаний, "А1_УниверсальнаяФорма__ОбработчикиКоманд", "");
		ДобавитьРеквизитыИЭлементы(Форма, МассивОписаний);		

		Форма.А1_УниверсальнаяФорма__ОбработчикиЭлементов = Новый Структура;
		Форма.А1_УниверсальнаяФорма__ОбработчикиКоманд = Новый Структура;
	КонецФункции
	
	Функция ДобавитьРеквизитыИЭлементы(Форма, МассивОписанийОбъектовФормы) Экспорт
		МассивРеквизитов = Новый Массив;
		Для Каждого ОписаниеРеквизита Из МассивОписанийОбъектовФормы Цикл
			А1Э_Формы.ДобавитьРеквизитВМассивПоОписанию(Форма, МассивРеквизитов, ОписаниеРеквизита);
		КонецЦикла;
		Форма.ИзменитьРеквизиты(МассивРеквизитов);
		
		Для Каждого ОписаниеКоманды Из МассивОписанийОбъектовФормы Цикл
			СоздатьКомандуПоОписанию(Форма, ОписаниеКоманды);
		КонецЦикла;
		
		Для Сч = 0 По МассивОписанийОбъектовФормы.Количество() - 1 Цикл
			ОписаниеЭлементаФормы = МассивОписанийОбъектовФормы[Сч]; 
			СоздатьЭлементПоОписанию(Форма, ОписаниеЭлементаФормы);
			А1Э_Формы.СоздатьКнопкуПоОписанию(Форма, ОписаниеЭлементаФормы);
		КонецЦикла;	
	КонецФункции
	
	Функция СоздатьЭлементПоОписанию(Форма, ОписаниеЭлементаФормы)
		ЭлементФормы = А1Э_Формы.СоздатьЭлементПоОписанию(Форма, ОписаниеЭлементаФормы);
		Если ЭлементФормы = Неопределено Тогда Возврат Неопределено КонецЕсли;
		
		Для Каждого Действие Из ОписаниеЭлементаФормы.Действия Цикл
			ЭлементФормы.УстановитьДействие(Действие.Ключ, "А1_ОбработчикУниверсальнойФормы__" + Действие.Ключ);
			Если А1Э_Структуры.ЗначениеСвойства(Форма.А1_УниверсальнаяФорма__ОбработчикиЭлементов, ЭлементФормы.Имя) = Неопределено Тогда
				Форма.А1_УниверсальнаяФорма__ОбработчикиЭлементов.Вставить(ЭлементФормы.Имя, Новый Структура);
			КонецЕсли;
			Форма.А1_УниверсальнаяФорма__ОбработчикиЭлементов[ЭлементФормы.Имя].Вставить(Действие.Ключ, Действие.Значение);
		КонецЦикла;
		
		Возврат ЭлементФормы;
	КонецФункции
	
	Функция СоздатьКомандуПоОписанию(Форма, ОписаниеКоманды)
		Команда = А1Э_Формы.СоздатьКомандуПоОписанию(Форма, ОписаниеКоманды);
		Если Команда = Неопределено Тогда Возврат Неопределено КонецЕсли;
		
		Команда.Действие = "А1_ОбработчикКомандУниверсальнойФормы";
		Форма.А1_УниверсальнаяФорма__ОбработчикиКоманд.Вставить(ОписаниеКоманды.Имя, ОписаниеКоманды.Действие);
	КонецФункции 
	
#КонецЕсли	

#Если Клиент Тогда
	
	Функция ВыполнитьОбработчикЭлемента(Форма, Элемент, ИмяОбработчика, Параметр1 = "НЕ_БУДЕТ_ДОБАВЛЕНО", Параметр2 = "НЕ_БУДЕТ_ДОБАВЛЕНО", Параметр3 = "НЕ_БУДЕТ_ДОБАВЛЕНО", Параметр4 = "НЕ_БУДЕТ_ДОБАВЛЕНО", Параметр5 = "НЕ_БУДЕТ_ДОБАВЛЕНО") Экспорт
		ОбработчикиЭлемента = А1Э_Структуры.ЗначениеСвойства(Форма.А1_УниверсальнаяФорма__ОбработчикиЭлементов, Элемент.Имя);
		Если ОбработчикиЭлемента = Неопределено Тогда Возврат Неопределено КонецЕсли;
		
		ПроцедураОбработчика = А1Э_Структуры.ЗначениеСвойства(ОбработчикиЭлемента, ИмяОбработчика);
		Если ПроцедураОбработчика = Неопределено Тогда Возврат Неопределено КонецЕсли;
		
		А1Э_Общее.РезультатПроцедуры(ПроцедураОбработчика, Форма, Элемент, Параметр1, Параметр2, Параметр3, Параметр4, Параметр5);
	КонецФункции 

#КонецЕсли
 
